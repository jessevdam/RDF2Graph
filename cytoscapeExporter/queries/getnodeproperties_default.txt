PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX core: <http://purl.uniprot.org/core/>		
PREFIX RDF2Graph: <http://ssb.wur.nl/RDF2Graph/>	
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT ?source2 ?sourceName ?count ?childInstanceCount ?label ?comment ?source 
FROM <http://ssb.wur.nl/RDF2Graph/>
WHERE
{
  ?source a RDF2Graph:Class .
  OPTIONAL
  {
    ?source RDF2Graph:count ?count .
  }
  OPTIONAL
  {
    ?source RDF2Graph:subClassOfInstanceCount ?childInstanceCount .
  }
  BIND (replace(str(?source),".*[/#]","") as ?sourceName)
  BIND(str(?source) as ?source2)
  OPTIONAL
  {
    ?source rdfs:label ?labelTemp .
  }
  OPTIONAL
  {
    ?source rdfs:comment ?comment .
  }
  BIND(IF(bound(?labelTemp),?labelTemp,?sourceName) as ?label)
}
ORDER BY ?sourceName